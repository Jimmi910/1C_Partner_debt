
#Область ПрограммныйИнтерфейс

#Область ЗаполнениеОбработчиковФормы

&После("ПриСозданииНаСервере")
Процедура PartnerDebt_ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Форма.ИмяФормы = "Справочник.Партнеры.Форма.ФормаЭлемента" Тогда
		PartnerDebt_ДополнитьФормуЭлементаПартнера(Форма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура PartnerDebt_ДополнитьФормуЭлементаПартнера(Форма)
	
	ТекущийЛимит = 0;
	
	Если Не Форма.Объект.Ссылка.Пустая() Тогда
	
		ТекущийЛимит = PartnerDebt_КонтрольДолгаПартнера.ЛимитДолгаПартнера(Форма.Объект.Ссылка, ТекущаяДатаСеанса());
		
	КонецЕсли;
	
	Валюта = Константы.ВалютаРегламентированногоУчета.Получить().Наименование;
	
	СтраницаДолга = PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздСтраница(Форма,
		"PartnerDebt_СтраницаЛимитДолгаПартнера", "ГруппаСтраницы", "Лимит долга");
	
	Группа = PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздГруппаОбычнаяБезОтображения(Форма,
		"PartnerDebt_ЛимитГоризонтально", СтраницаДолга);
	Группа.РастягиватьПоГоризонтали = Ложь;
	
	PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздДекорацияНадпись(Форма, "PartnerDebt_ТекущийЛимитДолга",
		Группа, "Лимит долга составляет: " + Строка(ТекущийЛимит) + " " + Валюта);
	
КонецПроцедуры

#КонецОбласти