
#Область ПрограммныйИнтерфейс

#Область ЗаполнениеОбработчиковФормы

&После("ПриСозданииНаСервере")
Процедура PartnerDebt_ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Форма.ИмяФормы = "Справочник.Партнеры.Форма.ФормаЭлемента" Тогда
		PartnerDebt_ДополнитьФормуЭлементаПартнера(Форма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура PartnerDebt_ДополнитьФормуЭлементаПартнера(Форма)
	
	Если Константы.PartnerDebt_КонтрольЛимитаДолгаПартнера.Получить() = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийЛимит = 0;
	
	Если Не Форма.Объект.Ссылка.Пустая() Тогда
	
		ТекущийЛимит = PartnerDebt_КонтрольДолгаПартнера.ЛимитДолгаПартнера(Форма.Объект.Ссылка, ТекущаяДатаСеанса());
		
	КонецЕсли;
	
	Валюта = Константы.ВалютаРегламентированногоУчета.Получить().Наименование;
	
	СтраницаДолга = PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздСтраница(Форма,
		"PartnerDebt_СтраницаЛимитДолгаПартнера", "ГруппаСтраницы", "Лимит долга");
	
	ТипЛимита = PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздПоле(Форма, "PartnerDebt_ТипЛимитаДолга", СтраницаДолга,
		"Тип расчета лимита долга", 1, "Объект.PartnerDebt_ТипУстановкиЛимитаДолга");
	ТипЛимита.АвтоМаксимальнаяШирина = Ложь;
	ТипЛимита.МаксимальнаяШирина = 20;
	ТипЛимита.УстановитьДействие("ПриИзменении", "Подключаемый_ТипУстановкиЛимитаДолгаПриИзменении");
	
	Группа = PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздГруппаОбычнаяБезОтображения(Форма,
		"PartnerDebt_ЛимитГоризонтально", СтраницаДолга);
	Группа.РастягиватьПоГоризонтали = Ложь;
	
	PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздДекорацияНадпись(Форма, "PartnerDebt_ТекущийЛимитДолга",
		Группа, "Лимит долга составляет: " + Строка(ТекущийЛимит) + " " + Валюта);
		
	ИмяКоманды = "PartnerDebt_УстановитьЛимитДолга";
	PartnerDebt_ПрограммнаяРаботаСФормойСервер.СоздатьКоманду(Форма, ИмяКоманды,
		"Установить лимит долга", "Подключаемый_PartnerDebt_УстановитьЛимитДолга");
	PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздКнопка(Форма, Строка(ИмяКоманды), Группа,
		"Установить лимит долга", Строка(ИмяКоманды));
	
	ИмяКоманды = "PartnerDebt_ПосмотретьИсториюИзмененийРучногоЛимита";
	PartnerDebt_ПрограммнаяРаботаСФормойСервер.СоздатьКоманду(Форма, ИмяКоманды,
		"История изменений ручных лимитов", "Подключаемый_PartnerDebt_ПосмотретьИсториюИзмененийРучногоЛимита");
	PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздКнопка(Форма, Строка(ИмяКоманды), Группа,
		"История изменений ручных лимитов", Строка(ИмяКоманды));
	
КонецПроцедуры

#КонецОбласти