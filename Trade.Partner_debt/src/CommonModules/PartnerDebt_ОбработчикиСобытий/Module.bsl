
#Область ПрограммныйИнтерфейс

// Partner debt обработка проведения обработка проведения.
// 
// Параметры:
//  Источник - ДокументОбъект - Источник
//  Отказ - Булево - Отказ
//  РежимПроведения - РежимПроведенияДокумента - Режим проведения
Процедура PartnerDebt_ОбработкаПроведенияОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	
	Если Не ТипЗнч(Источник) = Тип("ДокументОбъект.ЗаказКлиента") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Если Не Константы.PartnerDebt_КонтрольЛимитаДолгаПартнера.Получить() Тогда
		УстановитьПривилегированныйРежим(Ложь);
		Возврат;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ЕстьСтатусОтгрузитьУТоваров(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если PartnerDebt_КонтрольДолгаПартнераВызовСервера.ЛимитДолгаПартнераПревышен(Источник) Тогда
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Есть статус отгрузить у товаров.
// 
// Параметры:
//  Источник - ДокументОбъект.ЗаказКлиента - Источник
// 
// Возвращаемое значение:
//  Булево - Есть статус отгрузить у товаров
Функция ЕстьСтатусОтгрузитьУТоваров(Источник)
	
	ЕстьСтатусОтгрузить = Ложь;
	
	Для Каждого ТекущаяСтрока Из Источник.Товары Цикл
		Если ТекущаяСтрока.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Отгрузить Тогда
			ЕстьСтатусОтгрузить = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьСтатусОтгрузить;
	
КонецФункции

#КонецОбласти

