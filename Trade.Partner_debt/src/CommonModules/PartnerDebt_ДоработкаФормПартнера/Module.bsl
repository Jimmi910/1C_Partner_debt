
#Область ПрограммныйИнтерфейс

Процедура ДополнитьФормуЭлементаПартнера(Форма) Экспорт
	
	Если Константы.PartnerDebt_КонтрольЛимитаДолгаПартнера.Получить() = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийЛимит = 0;
	
	Если Не Форма.Объект.Ссылка.Пустая() Тогда
	
		ТекущийЛимит = PartnerDebt_КонтрольДолгаПартнераВызовСервера.ЛимитДолгаПартнера(Форма.Объект.Ссылка, ТекущаяДатаСеанса());
		
	КонецЕсли;
	
	Валюта = PartnerDebt_КонтрольДолгаПартнераВызовСервера.НаименованиеВалютыУправленческогоУчета();
	
	СтраницаДолга = PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздСтраница(Форма,
		"PartnerDebt_СтраницаЛимитДолгаПартнера", "ГруппаСтраницы", "Лимит долга");
	
	ТипЛимита = PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздПоле(Форма, "PartnerDebt_ТипЛимитаДолга", СтраницаДолга,
		"Тип расчета лимита долга", 1, "Объект.PartnerDebt_ТипУстановкиЛимитаДолга");
	ТипЛимита.АвтоМаксимальнаяШирина = Ложь;
	ТипЛимита.МаксимальнаяШирина = 20;
	ТипЛимита.УстановитьДействие("ПриИзменении", "Подключаемый_ТипУстановкиЛимитаДолгаПриИзменении");
	
	Группа = PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздГруппаОбычнаяБезОтображения(Форма,
		"PartnerDebt_ЛимитГоризонтально", СтраницаДолга);
	Группа.РастягиватьПоГоризонтали = Ложь;
	
	ИмяДекорации = PartnerDebt_КонтрольДолгаПартнераВызовСервера.ИмяЭлементаНадписиДолга();
	
	PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздДекорацияНадпись(Форма, ИмяДекорации,
		Группа, "Лимит долга составляет: " + Строка(ТекущийЛимит) + " " + Валюта);
		
	ИмяКоманды = "PartnerDebt_УстановитьЛимитДолга";
	PartnerDebt_ПрограммнаяРаботаСФормойСервер.СоздатьКоманду(Форма, ИмяКоманды,
		"Установить лимит долга", "Подключаемый_PartnerDebt_УстановитьЛимитДолга");
	PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздКнопка(Форма, Строка(ИмяКоманды), Группа,
		"Установить лимит долга", Строка(ИмяКоманды));
	
	ИмяКоманды = "PartnerDebt_ПосмотретьИсториюИзмененийРучногоЛимита";
	PartnerDebt_ПрограммнаяРаботаСФормойСервер.СоздатьКоманду(Форма, ИмяКоманды,
		"История изменений ручных лимитов", "Подключаемый_PartnerDebt_ПосмотретьИсториюИзмененийРучногоЛимита");
	PartnerDebt_ПрограммнаяРаботаСФормойСервер.СздКнопка(Форма, Строка(ИмяКоманды), Группа,
		"История изменений ручных лимитов", Строка(ИмяКоманды));
	
КонецПроцедуры

#КонецОбласти
