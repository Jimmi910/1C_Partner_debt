
#Область ОбработчикиСобытийФормы

&После("ПриОткрытии")
&НаКлиенте
Процедура PartnerDebt_ПриОткрытии(Отказ)
	PartnerDebt_УстановитьВидимость();
КонецПроцедуры

&После("ПослеЗаписи")
&НаКлиенте
Процедура PartnerDebt_ПослеЗаписи(ПараметрыЗаписи)
	PartnerDebt_ОбновитьНадписьЛимитаДолга();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_ТипУстановкиЛимитаДолгаПриИзменении(Элемент)
	PartnerDebt_УстановитьВидимость();
	PartnerDebt_ОбновитьНадписьЛимитаДолга();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_PartnerDebt_УстановитьЛимитДолга(Команда)
	
	Если Объект.Ссылка.Пустая() ИЛИ Модифицированность Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Необходимо записать Партнера!");
		Возврат;
	КонецЕсли;
	
	ПослеВводаЛимита = Новый ОписаниеОповещения("PartnerDebt_ПослеВводаЛимита", ЭтотОбъект);
	
	ПоказатьВводЧисла(ПослеВводаЛимита, 0, "Введите новый лимит", 17, 2);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_PartnerDebt_ПосмотретьИсториюИзмененийРучногоЛимита(Команда)
	
	ОтборФормы = Новый Структура;
	ОтборФормы.Вставить("Партнер", Объект.Ссылка);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", ОтборФормы);
	
	ОткрытьФорму("РегистрСведений.PartnerDebt_РучныеЛимитыДолгаПартнера.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура PartnerDebt_ПослеВводаЛимита(Значение, ДополнительныеПараметры) Экспорт
	
	Если Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Значение >= 0 Тогда
		PartnerDebt_УстановитьНовыйЛимитДолга(Значение, Объект.Ссылка);
		PartnerDebt_ОбновитьНадписьЛимитаДолга();
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Нельзя установить отрицательное значение лимита!");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура PartnerDebt_ОбновитьНадписьЛимитаДолга()
	
	Если Константы.PartnerDebt_КонтрольЛимитаДолгаПартнера.Получить() = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Лимит = PartnerDebt_КонтрольДолгаПартнера.ЛимитДолгаПартнера(Объект.Ссылка, ТекущаяДатаСеанса());
	
	РегВалюта = Константы.ВалютаРегламентированногоУчета.Получить().Наименование;
	Элементы["PartnerDebt_ТекущийЛимитДолга"].Заголовок = "Лимит долга составляет: " + Строка(Лимит) + " " + РегВалюта;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура PartnerDebt_УстановитьНовыйЛимитДолга(Значение, Партнер)
	PartnerDebt_КонтрольДолгаПартнера.УстановитьНовыйЛимитДолга(Значение, Партнер);
КонецПроцедуры

&НаСервере
Процедура PartnerDebt_УстановитьВидимость()

	Если Константы.PartnerDebt_КонтрольЛимитаДолгаПартнера.Получить() = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	РучнойЛимит = Объект.PartnerDebt_ТипУстановкиЛимитаДолга = Перечисления.PartnerDebt_ТипУстановкиЛимитаДолга.Ручной;
	
	Элементы["PartnerDebt_УстановитьЛимитДолга"].Видимость = РучнойЛимит;
	Элементы["PartnerDebt_ПосмотретьИсториюИзмененийРучногоЛимита"].Видимость = РучнойЛимит;
	
КонецПроцедуры

#КонецОбласти